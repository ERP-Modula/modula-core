plugins {
	id 'java'
	id 'maven-publish'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.modula'
version = '1.0.0'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'

	compileOnly 'org.projectlombok:lombok:1.18.36'
	annotationProcessor 'org.projectlombok:lombok:1.18.36'

	testCompileOnly 'org.projectlombok:lombok:1.18.36'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'

	implementation 'org.mapstruct:mapstruct:1.5.3.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'
}

tasks.named('test') {
	useJUnitPlatform()
}

bootJar {
	enabled = false
}

jar {
	enabled = true
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			pom {
				name = 'common-lib'
				description = 'Common library with shared DTOs, utils, etc.'
				url = 'https://github.com/ERP-Modula/modula-core'
				licenses {
					license {
						name = 'Apache License 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/ERP-Modula/modula-core.git'
					developerConnection = 'scm:git:ssh://github.com:ERP-Modula/modula-core.git'
					url = 'https://github.com/ERP-Modula/modula-core'
				}
			}
		}
	}

	repositories {
		maven {
			name = 'GitHubPackages'
			url = uri("https://maven.pkg.github.com/ERP-Modula/modula-core")

			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USER")
				password = project.findProperty("gpr.key")  ?: System.getenv("GITHUB_TOKEN")
			}
		}
	}
}